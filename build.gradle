plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.gradleup.shadow" version "8.3.0"
}

group = 'de.mecrytv'
version = '1.0.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.triumphteam.co/namespaces/maven-public/"
    }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    compileOnly("com.arcaniax:HeadDatabase-API:1.3.2")
    compileOnly("net.luckperms:api:5.4")
    implementation 'de.mecrytv:LanguageAPI:1.0.0'
    implementation 'de.mecrytv:DatabaseAPI:1.2.0'
    implementation "dev.triumphteam:triumph-gui-paper:3.1.13"
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    archiveBaseName.set('Nexus-Core')
    archiveClassifier.set('')
    archiveVersion.set('')

    relocate 'dev.triumphteam.gui', 'de.mecrytv.libs.gui'
    relocate 'de.mecrytv.languageapi', 'de.mecrytv.libs.languageapi'
    relocate 'de.mecrytv.databaseapi', 'de.mecrytv.libs.databaseapi'
}

tasks {
    jar { enabled = false }
    runServer { minecraftVersion("1.21.10") }
    build { dependsOn shadowJar }
}

def targetJavaVersion = 22
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
